@advice(QM1)
rtVilScript startup (Project source, Configuration config, Project target, IAdaptationEvent event) {

    strategy main(Project source, Configuration config, Project target, IAdaptationEvent event) = {
        objective true;
        breakdown {
            tactic defaultTactic();
        }
    }

    strategy startup(Project source, Configuration config, Project target, LifecycleEvent event) = : event.status() == LifecycleEventStatus.START {
        objective true;
        breakdown {
            tactic startupTakeFirst(config, event.getPipeline());
        }
    }

    tactic defaultTactic() = {
    }

    tactic startupTakeFirst(Configuration config, String pipName) = {
        QM1 qm = config;
        for(Pipeline pip:qm.pipelines) {
            if (pip.name == pipName) {
                for(Source src:pip.sources) {
                    assignFirst(src);
                };
            }
        };
    }

    assignFirst(Node node) = : {
    }

    assignFirst(Source src) = : {
        for(Node n:src.next) {
            assignFirst(n);
        };
    }

    assignFirst(FamilyElement elt) = : {
        Family fam = elt.family;
        if (!elt.actual.isConfigured()) {
            elt.actual = fam.members.toSequence().first();
        }
        for(Node n:elt.next) {
            assignFirst(n);
        };
    }

    enact(Project source, Configuration config, Project target) = : {
        QM1 qm = config.selectChangedWithContext();
        for(Pipeline p:qm.pipelines) {
            for(Source s:p.sources) {
                enact(p, s);
            };
        };
    }

    enact(Pipeline pipeline, Node node) = : {
    }

    enact(Pipeline pipeline, Source source) = : {
        for(Node n:source.next) {
            enact(pipeline, n);
        };
    }

    enact(Pipeline pipeline, FamilyElement elt) = : {
        Family family = elt.family;
        CommandSequence cmd = new CommandSequence();
        if (elt.actual != null) {
            cmd.add(new AlgorithmChangeCommand(pipeline.name, elt.name, elt.actual.name));
        }
        for(Node n:elt.next) {
            enact(pipeline, n);
        };
        cmd.exec();
    }

}